import("//llvm/utils/gn/build/symlink_or_copy.gni")

symlinks = [
  "lld-link",
  "ld.lld",
  "ld64.lld",
  "ld64.lld.darwinnew",
  "wasm-ld",
]
foreach(target, symlinks) {
  symlink_or_copy(target) {
    deps = [ ":lld" ]
    source = "lld"
    output = "$root_out_dir/bin/$target"
  }
}

# //:lld depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = []
  foreach(target, symlinks) {
    deps += [ ":$target" ]
  }
}

executable("lld") {
  configs += [ "//llvm/utils/gn/build:lld_code" ]
  deps = [
    "//lld/COFF",
    "//lld/ELF",
    "//lld/MachO:MachO2",
    "//lld/MinGW",
    "//lld/lib/Driver",
    "//lld/wasm",
    "//llvm/lib/Support",
  ]
  sources = [ "lld.cpp" ]
}
