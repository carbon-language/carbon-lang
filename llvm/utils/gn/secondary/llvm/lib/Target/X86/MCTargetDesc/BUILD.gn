import("//llvm/utils/TableGen/tablegen.gni")

tablegen("X86GenAsmWriter") {
  visibility = [ ":tablegen" ]
  args = [ "-gen-asm-writer" ]
  td_file = "../X86.td"
}

tablegen("X86GenAsmWriter1") {
  visibility = [ ":tablegen" ]
  args = [
    "-gen-asm-writer",
    "-asmwriternum=1",
  ]
  td_file = "../X86.td"
}

tablegen("X86GenInstrInfo") {
  visibility = [ ":tablegen" ]
  args = [ "-gen-instr-info" ]
  td_file = "../X86.td"
}

tablegen("X86GenRegisterInfo") {
  visibility = [ ":tablegen" ]
  args = [ "-gen-register-info" ]
  td_file = "../X86.td"
}

tablegen("X86GenSubtargetInfo") {
  visibility = [ ":tablegen" ]
  args = [ "-gen-subtarget" ]
  td_file = "../X86.td"
}

group("tablegen") {
  visibility = [
    ":MCTargetDesc",
    "../TargetInfo",
  ]
  public_deps = [
    ":X86GenAsmWriter",
    ":X86GenAsmWriter1",
    ":X86GenInstrInfo",
    ":X86GenRegisterInfo",
    ":X86GenSubtargetInfo",
  ]
}

static_library("MCTargetDesc") {
  output_name = "LLVMX86Desc"
  public_deps = [
    ":tablegen",
  ]
  deps = [
    "//llvm/lib/MC",
    "//llvm/lib/MC/MCDisassembler",
    "//llvm/lib/Object",
    "//llvm/lib/Support",
    "//llvm/lib/Target/X86/TargetInfo",
    "//llvm/lib/Target/X86/Utils",
  ]
  include_dirs = [ ".." ]
  sources = [
    "X86ATTInstPrinter.cpp",
    "X86AsmBackend.cpp",
    "X86ELFObjectWriter.cpp",
    "X86InstComments.cpp",
    "X86InstPrinterCommon.cpp",
    "X86IntelInstPrinter.cpp",
    "X86MCAsmInfo.cpp",
    "X86MCCodeEmitter.cpp",
    "X86MCTargetDesc.cpp",
    "X86MachObjectWriter.cpp",
    "X86WinCOFFObjectWriter.cpp",
    "X86WinCOFFStreamer.cpp",
    "X86WinCOFFTargetStreamer.cpp",
  ]
}
