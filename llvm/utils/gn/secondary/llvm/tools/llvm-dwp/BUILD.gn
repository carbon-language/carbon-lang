import("//llvm/tools/binutils_symlinks.gni")
import("//llvm/utils/gn/build/symlink_or_copy.gni")

if (llvm_install_binutils_symlinks) {
  symlink_or_copy("dwp") {
    deps = [
      ":llvm-dwp",
    ]
    source = "llvm-dwp"
    output = "$root_out_dir/bin/dwp"
  }
}

# //:llvm-dwp depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [
    ":llvm-dwp",
  ]
  if (llvm_install_binutils_symlinks) {
    deps += [ ":dwp" ]
  }
}

executable("llvm-dwp") {
  deps = [
    "//llvm/lib/CodeGen/AsmPrinter",
    "//llvm/lib/DebugInfo/DWARF",
    "//llvm/lib/MC",
    "//llvm/lib/Object",
    "//llvm/lib/Support",
    "//llvm/lib/Target",
    "//llvm/lib/Target:TargetsToBuild",
  ]
  sources = [
    "DWPError.cpp",
    "llvm-dwp.cpp",
  ]
}
