import("//llvm/utils/gn/build/write_vcsrevision.gni")

config("write_vcsrevision_config") {
  # To pick up the generated inc file.
  include_dirs = [ target_gen_dir ]
  visibility = [ ":write_vcsversion" ]
}

write_vcsrevision("write_vcsversion") {
  visibility = [ ":lldbBase" ]
  header = "$target_gen_dir/VCSVersion.inc"
  names = [ "LLDB" ]
  public_configs = [ ":write_vcsrevision_config" ]
}

static_library("lldbBase") {
  configs += [
    "//llvm/utils/gn/build:lldb_code",

    # To pick up clang/Basic/Version.h.
    "//llvm/utils/gn/build:clang_code",
  ]
  sources = [ "lldb.cpp" ]
  deps = [
    ":write_vcsversion",
    "//clang/lib/Basic",
  ]

  # XXX define LLDB_VERSION_STRING
}
